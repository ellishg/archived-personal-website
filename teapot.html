<!DOCTYPE HTML>
<html lang="en">
  <head>
    <title>Reflective Teapot</title>
    <meta charset="utf-8">
  </head>

  <script id="skybox_vertex_shader" type="x-shader/x-vertex">
    precision mediump float;

    uniform mat4 modelview_matrix;
    uniform mat4 projection_matrix;

    attribute vec3 vertex_position;

    varying vec3 varying_tex_coord;

    void main()
    {
      varying_tex_coord = vertex_position;
      gl_Position = projection_matrix * modelview_matrix * vec4(vertex_position, 1.0);
    }
  </script>

  <script id="skybox_fragment_shader" type="x-shader/x-fragment">
    precision mediump float;

    varying vec3 varying_tex_coord;

    uniform samplerCube sampler;

    void main()
    {
      gl_FragColor = textureCube(sampler, varying_tex_coord);
    }
  </script>

  <script id="teapot_vertex_shader" type="x-shader/x-vertex">
    precision mediump float;

    uniform mat4 model_matrix;
    uniform mat4 view_matrix;
    uniform mat4 normal_model_matrix;
    uniform mat4 normal_view_matrix;
    uniform mat4 projection_matrix;

    attribute vec3 vertex_position;
    attribute vec3 normal;

    varying vec4 varying_worldview_position;
    varying vec4 varying_normal;

    void main()
    {
      varying_normal = /*normal_view_matrix */ normal_model_matrix * vec4(normal, 0.0);

      varying_worldview_position = view_matrix * model_matrix * vec4(vertex_position, 1.0);

      gl_Position = projection_matrix * varying_worldview_position;

      varying_worldview_position = varying_worldview_position / varying_worldview_position.w;
    }
  </script>

  <script id="teapot_fragment_shader" type="x-shader/x-fragment">
    precision mediump float;

    uniform samplerCube sampler;

    uniform mat4 normal_view_matrix;

    uniform mat4 skybox_view;

    uniform vec3 diffuse_color;
    uniform vec3 specular_color;

    uniform vec3 light_source;

    uniform float reflectiveness;
    uniform float shininess;

    varying vec4 varying_worldview_position;
    varying vec4 varying_normal;

    void main()
    {
      vec3 normal = normalize(varying_normal.xyz);

      vec3 light_dir = normalize(light_source - varying_worldview_position.xyz);

      float lambertian = max(dot(light_dir, normal), 0.0);

      float specular = 0.0;
      if (lambertian > 0.0)
      {
        vec3 view_dir = normalize(-varying_worldview_position.xyz);

        vec3 light_reflect = reflect(-light_dir, normal);

        specular = pow(max(dot(light_reflect, view_dir), 0.0), shininess);
      }

      vec4 view_reflect = normal_view_matrix * vec4(reflect(varying_worldview_position.xyz, normal), 0.0);
      vec3 reflected_color = textureCube(sampler, view_reflect.xyz).xyz * reflectiveness;

      gl_FragColor = vec4(reflected_color + lambertian * diffuse_color + specular * specular_color, 1.0);
    }
  </script>

  <script src="assets/js/gl-matrix.js"></script>
  <script src="assets/js/webgl-utils.js"></script>
  <script src="assets/js/environment.js"></script>

  <body onload="startup();">
    <canvas id="myGLCanvas" width="500" height="500"></canvas>
    <p><b>Welcome to my shiny teapot!</b></p>
    <p>
      Use the arrow keys to control the teapot. Use the A/D arrow keys to control the shininess. Use the W/S arrow keys to control the reflectiveness.
    </p>
  </body>
</html>
